/**
 *    Copyright 2023 Sven Loesekann
   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at
       http://www.apache.org/licenses/LICENSE-2.0
   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
 */
 plugins {
	id 'java'
}
 
group = 'ch.xxx'
version = '0.0.1-SNAPSHOT'

task cleanAngular {
    onlyIf {
        project.hasProperty('withAngular')
    }
    doLast {
        println('Task cleanAngular')
        delete('src/angular/node_modules')
    }
}

task cleanDist {
    onlyIf {
        project.hasProperty('withAngular')
    }
    doLast {
        delete('src/angular/dist')
    }
}

task createDist {
    onlyIf {
        project.hasProperty('withAngular')
    }
    doLast {
        mkdir('src/angular/dist')
    }
}

task npmInstall(type: Exec) {
    onlyIf {
        project.hasProperty('withAngular')
    }
    workingDir = file('src/angular')
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine 'npm.cmd', 'install'
    } else {
        commandLine 'npm', 'install'
    }
    dependsOn cleanAngular
}

task npmBuild(type: Exec) {
    onlyIf {
        project.hasProperty('withAngular')
    }
    workingDir = file('src/angular')
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine 'npm.cmd', 'run', 'build'
    } else {
        commandLine 'npm', 'run', 'build'
    }
    dependsOn npmInstall, cleanDist, createDist
}

task testAngular(type: Exec) {
    onlyIf {
        project.hasProperty('withAngular')
    }
    workingDir = file('src/angular')
    if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
        commandLine 'npm.cmd', 'run', 'test'
    } else {
        if(project.hasProperty("useChromium")) {
            commandLine 'npm', 'run', 'test-chromium'
        } else {
            commandLine 'npm', 'run', 'test'
        }
    }
    dependsOn npmInstall
    doFirst {
        println("Running ${commandLine.join(' ')}")
    }
}

task buildAngular {
    dependsOn npmBuild, testAngular
}

// Make sure the main build task depends on buildAngular
build.dependsOn buildAngular
